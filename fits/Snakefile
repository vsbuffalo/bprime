# main annotation files

# other params
NCORES = 40
NCORES_BOOT = 40
NSTARTS = 1000
NSTARTS_BOOT = 500

NCORES_LOO = NCORES
NSTARTS_LOO = 50

CHROMS = [f'chr{x}' for x in range(1, 23)]

# from config
name = config["name"]
model = config["model"]
mu = config.get("mu", None)
seqlens_file = config["seqlens_file"]
recmap_file = config["recmap_file"]
access_file = config["access_file"]
neut_file = config["neut_file"]
fasta_file = config["fasta_file"]
counts_dir = config["counts_dir"]
Bs_file = config["Bs_file"]
windows = [int(w) for w in config["windows"]]
blocksize = int(config["blocksize"])


rule fit:
  input: seqlens=seqlens_file, recmap_file=recmap_file,
         access_file=access_file, fasta_file=fasta_file,
         neut_file=neut_file, counts_dir=counts_dir,
         bs_file=Bs_file
  output: f'{name}/window_{{window}}/mle.pkl'
  params: ncores=NCORES, nstarts=NSTARTS, model=model, mu=mu
  shell:
    """
    bgspy loglik --seqlens {input.seqlens} --recmap {input.recmap_file} \
      --model {params.model} --mu {params.mu} \
      --counts-dir {input.counts_dir} --neutral {input.neut_file} \
      --access {input.access_file} --bs-file {input.bs_file} \
      --fasta {input.fasta_file} \
      --ncores {params.ncores} --nstarts {params.nstarts} \
      --window {wildcards.window} \
      --outfile {output[0]}
    """

rule bootstrap:
  # note that this only runs one bootstrap, we set nboot = ncores
  input: fit=f'{name}/window_{{window}}/mle.pkl', 
         seqlens=seqlens_file, recmap_file=recmap_file,
         access_file=access_file, fasta_file=fasta_file,
         neut_file=neut_file, counts_dir=counts_dir,
         bs_file=Bs_file
  output: f'{name}/window_{{window}}/bootstraps/bootstrap_{{boot}}.npz'
  params: ncores=NCORES_BOOT, nstarts=NSTARTS_BOOT, blocksize=blocksize
  shell:
    """
    bgspy bootstrap --fit {input.fit} \
      --B {params.ncores} --blocksize {params.blocksize} \
      --seqlens {input.seqlens} --recmap {input.recmap_file} \
      --counts-dir {input.counts_dir} --neutral {input.neut_file} \
      --access {input.access_file} --bs-file {input.bs_file} \
      --fasta {input.fasta_file} \
      --ncores {params.ncores} --nstarts {params.nstarts} \
      --window {wildcards.window} \
      --outfile {output[0]}
    """

rule loo:
  input: fit=f'{name}/window_{{window}}/mle.pkl', 
         seqlens=seqlens_file, recmap_file=recmap_file,
         access_file=access_file, fasta_file=fasta_file,
         neut_file=neut_file, counts_dir=counts_dir,
         bs_file=Bs_file
  output: f'{name}/window_{{window}}/loo/loo_r2_{{chrom}}.npz'
  params: ncores=NCORES_LOO, nstarts=NSTARTS_LOO
  shell:
    """
    bgspy r2 --fit {input.fit} \
      --r2-file {output[0]} \
      --fit-dir loo \
      --loo-chrom {wildcards.chrom} \
      --ncores {params.ncores} --nstarts {params.nstarts}
    """

NBOOT_PARALLEL = 100
all_results = []
for window in windows:
  all_results.append(f'{name}/window_{window}/mle.pkl')
  boots = [f"{name}/window_{window}/bootstraps/bootstrap_{b}.npz" for 
           b in range(NBOOT_PARALLEL)]
  all_results.extend(boots)
  for chrom in CHROMS:
      loo_r2 = f"{name}/window_{window}/loo/loo_r2_{chrom}.npz"
      all_results.append(loo_r2)

rule all:
  input: all_results
