if not len(config):
    raise ValueError("config file not specified on command line, use --configfile config.json")

# config file should have filename pattern mprime_bgs_{name}.json, 
# as it's loaded elsewhere here
name = config['name']

all_files = [f"../data/msprime_sims/msprime_bgs_{name}.npz", 
             f"../data/msprime_sims/msprime_bgs_{name}_data.pkl"]

archs = [dict(n64=0, n32=2),
         dict(n64=0, n32=4),
         dict(n64=4, n32=2),
         dict(n64=8, n32=4),
         dict(n64=8, n32=8),
         dict(n64=8, n32=0),
         ]

NREPS = 2
fits = []
for arch in archs:
    for rep in range(NREPS):
        kwargs = {**arch, 'rep': rep}
        fits.append(f"../data/msprime_sims/msprime_bgs_{name}_{{n64}}n64_{{n32}}n32_fit_{{rep}}rep.pkl".format(**kwargs))

all_files.extend(fits)

rule msprime:
  input: f"msprime_bgs_{name}.json"
  output: f"../data/msprime_sims/msprime_bgs_{name}.npz"
  params: nsamples=500_000, ncores=70
  shell:
    """
    python ../tools/msprime_bgs.py --nsamples {params.nsamples} --ncores {params.ncores} --outfile {output} --func 'segment' {input}
    """

rule process_data:
  input: json=f"msprime_bgs_{name}.json", npz=f"../data/msprime_sims/msprime_bgs_{name}.npz"
  output: f"../data/msprime_sims/msprime_bgs_{name}_data.pkl"
  shell:
     """
     python ../tools/fit_sims.py data {input.json} {input.npz}
     """

rule fit_dnn:
  input: f"../data/msprime_sims/msprime_bgs_{name}_data.pkl"
  output: f"../data/msprime_sims/msprime_bgs_{name}_{{n64}}n64_{{n32}}n32_fit_{{rep}}rep.pkl"
  shell:
     """
     python ../tools/fit_sims.py fit --outfile {output} --n64 {wildcards.n64} --n32 {wildcards.n32} {input} 
     """

rule all:
  input: all_files

rule clean:
  shell: f"rm -f {' '.join(all_files)}"

