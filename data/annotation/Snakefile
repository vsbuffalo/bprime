# WARNING: bedtools complement returns all the chromosomes!

AUTOS = list(range(1, 23))
AUTOSEX = AUTOS + ['X', 'Y']
PHASTCONS_THRESH_10 = 471
PHASTCONS_THRESH_5 = 527

## === Human genome data

genome = ["hg38.fa.gz"]

rule hg38:
  output: "hg38.fa.gz"
  shell:
    """
    wget -O {output} http://ftp.ensembl.org/pub/release-107/fasta/homo_sapiens/dna/Homo_sapiens.GRCh38.dna_sm.primary_assembly.fa.gz
    """

# only keep autosomes
rule seqlens:
  input: "hg38.fa.gz"
  output: "hg38_seqlens.tsv"
  shell:
    """
      bioawk -cfastx '{{ print $name,length($seq) }}' hg38.fa.gz | \
      sed 's/^/chr/' | grep -P '^chr[0-9X]+' |  \
      sort  --version-sort -k 1,1 -k2,2n > {output}
    """


## === Human Annotation Data
annot = ["hg38_seqlens.tsv", "phastConsElements100way.txt.gz", 
         "hg38.ensembl.gff3.gz",
         "hg38.ensembl_cds.bed.gz", 
         "hg38.ensembl_genes.bed.gz", "hg38_centro.bed", "ensembl_pseudogenes.bed.gz",
         "hg38.ensembl_exons.bed.gz", "hg38.ensembl_utrs.bed.gz", 
         "hg38.ensembl_introns_conservative.bed.gz", "hg38.ensembl_introns.bed.gz", 
         ]

rule centro:
  output: "hg38_centro.bed"
  shell:
    """
    curl http://hgdownload.cse.ucsc.edu/goldenPath/hg38/database/cytoBand.txt.gz | zgrep "acen" | grep -v "chrY" | sort --version-sort -k1,1 -k2,2n > {output}
    """

rule centro_slop:
  output: "hg38_centro_slop.bed"
  input: centro="hg38_centro.bed", seqlens="hg38_seqlens.tsv"
  shell:
    """
    bedtools slop -g {input.seqlens} -b 1000000 -i {input.centro} | sort --version-sort -k1,1 -k2,2n > {output}
    """

rule phastcons:
  output: "phastConsElements100way.txt.gz"
  shell:
    """
    curl http://hgdownload.soe.ucsc.edu/goldenPath/hg38/database/phastConsElements100way.txt.gz | \
     zcat | cut -f2- | grep -P '^chr[0-9X]+\t' | gzip > {output}
    """

rule phastcons_bed:
  input: "phastConsElements100way.txt.gz"
  output: "phastConsElements100way.bed.gz"
  shell:
    """
     zcat {input} | bioawk -t '{{ print $1,$2,$3 }}' | sort --version-sort -k1,1 -k2,2n | gzip > {output}
    """

rule phastcons_bed_filtered:
  output: "phastConsElements100way_thresh{thresh}.bed.gz"
  input: "phastConsElements100way.txt.gz"
  shell:
    """
     # Remove alt chroms and filter the phastcons by a score constant
     zcat {input} | bioawk -t ' $5 > {wildcards.thresh} {{ print $1,$2,$3 }}' | sort --version-sort -k1,1 -k2,2n |  gzip > {output}
    """

rule ensembl_gff:
  output: "hg38.ensembl.gff3.gz"
  shell:
     """
     curl http://ftp.ensembl.org/pub/release-107/gff3/homo_sapiens/Homo_sapiens.GRCh38.107.chr.gff3.gz | \
       zgrep -v '^#' | sed s/^/chr/ | \
       grep -P '^chr[0-9X]+' | \
       grep -v "chrY" | \
       gzip > {output}
     """

rule ensembl_pseudogenes:
   input: "hg38.ensembl.gff3.gz"
   output: "ensembl_pseudogenes.bed.gz"
   shell:
     """
     zgrep -v "#" {input} | bioawk -cgff ' $feature ~ /^pseudogene$/ {{ print "chr"$seqname,$start,$end,$feature }}' | \
      sort --version-sort -k1,1 -k2,2n |  \
      bedtools merge -c 4 -o distinct | sort --version-sort -k1,1 -k2,2n | gzip > {output}
     """

rule genes:
  input: "hg38.ensembl.gff3.gz"
  output: "hg38.ensembl_genes.bed.gz"
  shell:
    """
      bioawk -cgff ' $feature ~ /^gene$/ {{ print $seqname,$start,$end,$feature }} ' {input} | \
      sort --version-sort -k1,1 -k2,2n | gzip > {output}
    """

rule exons:
  input: "hg38.ensembl.gff3.gz"
  output: "hg38.ensembl_exons.bed.gz"
  shell:
    """
      bioawk -cgff ' $feature ~ /^exon$/ {{ print $seqname,$start,$end,$feature }} ' {input} | \
      sort --version-sort -k1,1 -k2,2n | gzip > {output}
    """

rule exons_merged:
  input: "hg38.ensembl_exons.bed.gz"
  output: "hg38.ensembl_exons_merged.bed.gz"
  shell:
    """
      bedtools merge -i {input} > {output}
    """

rule cds:
  input: "hg38.ensembl.gff3.gz"
  output: "hg38.ensembl_cds.bed.gz"
  shell:
    """
      bioawk -cgff ' $feature ~ /^CDS$/ {{ print $seqname,$start,$end,$feature }} ' {input} | \
      sort --version-sort -k1,1 -k2,2n | gzip > {output}
    """

rule utrs:
  input: "hg38.ensembl.gff3.gz"
  output: "hg38.ensembl_utrs.bed.gz"
  shell:
    """
      bioawk -cgff ' $feature ~ /five_prime_UTR|three_prime_UTR/ {{ print $seqname,$start,$end,$feature }}' {input} | \
      sort --version-sort -k1,1 -k2,2n | gzip > {output}
    """

rule introns:
  input: genes="hg38.ensembl_genes.bed.gz", utr="hg38.ensembl_utrs.bed.gz", exon="hg38.ensembl_exons_merged.bed.gz"
  output: "hg38.ensembl_introns.bed.gz"
  shell:
    """
    zcat {input.genes} | cut -f1-3 | bedtools merge | \
     bedtools subtract -a - -b {input.exon} | bedtools subtract -a -  -b {input.utr} | \
     bioawk -cbed '{{ print $1,$2,$3,"intron" }}' | \
     gzip > {output}
    """

rule introns_conservative:
  # take the introns and shrink them by 20bp, since 
  # splicing may conserve this -- a buffer
  input: "hg38.ensembl_introns.bed.gz"
  output: "hg38.ensembl_introns_conservative.bed.gz"
  params: buffer=20
  shell:
    """
    bioawk -cbed '{{ $start = $start - {params.buffer}; $end = $end - {params.buffer}; if ($end > $start) {{ print; }} }}' {input} |  gzip > {output}
    """


## === Genomic Windows
rule human_100kb:
  output: "hg38_100kb.bed"
  input: "hg38_seqlens.tsv"
  shell:
     """
     bedtools makewindows -g {input}  -w 100000 > {output}
     """


## === Human Recombination Maps

## ==== DeCode Recombination Map
decode = ["decode_sex_averaged.txt"]
rule decode_map:
  output: "decode_sex_averaged.txt"
  shell:
    """
    #wget https://www.decode.com/additional/sex-averaged.rmap
    wget https://stdpopsim.s3.us-west-2.amazonaws.com/genetic_maps/HomSap/DeCodeSexAveraged_GRCh36_liftedOverTo_Hg38.tar.gz
    tar -xvzf DeCodeSexAveraged_GRCh36_liftedOverTo_Hg38.tar.gz
    find DeCodeSexAveraged_GRCh36_liftedOverTo_Hg38 -name "*.txt" | sort --version-sort | xargs -n1 cat  | sed '2,${{/^Chr/d}}' > {output}
    rm -f DeCodeSexAveraged_GRCh36_liftedOverTo_Hg38.tar.gz
    """

## ==== HapMap Recombination Map 
hapmap_files = [f"HapMapII_GRCh37_liftedOverTo_Hg38/genetic_map_Hg38_{chrom}.txt" for chrom in AUTOS ]
hapmap = ["hapmap_genetic_map.txt"]

rule download_hapmap:
  output: "hapmap_genetic_map.txt"
  shell:
    """
    wget https://stdpopsim.s3.us-west-2.amazonaws.com/genetic_maps/HomSap/HapMapII_GRCh38.tar.gz
    mkdir -p HapMapII_GRCh38
    tar -xvzf HapMapII_GRCh38.tar.gz --directory HapMapII_GRCh38

    find HapMapII_GRCh38 -name "*.txt" | sort --version-sort | xargs -n1 cat  | sed '2,${{/^Chr/d}}' > {output}
    rm -f HapMapII_GRCh38.tar.gz
    """

## === Conservation Tracks
cons = [ # phastcons at different filtering thresholds:
        "conserved_phastcons_thresh471.bed.gz", 
        "conserved_phastcons_thresh527.bed.gz",
        "conserved_phastcons_thresh0.bed.gz",
        # merged feature track
        "merged_utrs_cds_introns_phastcons.bed.gz",
        # merged but no phastcons
        "merged_utrs_cds_introns.bed.gz",
        # merged and unlabelled (e.g. treated as all one feature type, for sims and model comparison)
        "merged_utrs_cds_introns_phastcons_unlabelled.bed.gz",
        "merged_utrs_cds_introns_unlabelled.bed.gz"
] 

# this is used for sims with combined conserved regions (phastcons)
rule conserved_track:
  input: phastcons="phastConsElements100way_thresh{thresh}.bed.gz"
  output: "conserved_phastcons_thresh{thresh,[0-9]+}.bed.gz"
  shell: 
      """
      # combine the files, sort and merge overlapping tracks
      # the messy grep filters out alt chromosome
      (zcat {input.phastcons} | \
        bioawk -t '{{ print $1,$2,$3,"phastcons" }}') | grep -P "^chr[0-9]+\t" | \
        sort  --version-sort -k 1,1 -k2,2n | \
        bedtools merge | gzip > {output}
      """

rule merged_utrs_cds_introns:
  input: utrs="hg38.ensembl_utrs.bed.gz", introns="hg38.ensembl_introns.bed.gz", cds="hg38.ensembl_cds.bed.gz", seqlens="hg38_seqlens.tsv"
  output: "merged_utrs_cds_introns.bed.gz"
  shell:
    """
    python ../../tools/combine_features.py --seqlens {input.seqlens} --bed utr {input.utrs} --bed intron {input.introns} --bed cds {input.cds} | \
      gzip > {output}
    """

rule merged_utrs_cds_introns_phastcons:
  input: utrs="hg38.ensembl_utrs.bed.gz", introns="hg38.ensembl_introns.bed.gz", cds="hg38.ensembl_cds.bed.gz", phastcons="conserved_phastcons_thresh0.bed.gz", seqlens="hg38_seqlens.tsv"
  output: "merged_utrs_cds_introns_phastcons.bed.gz"
  shell:
    """
    python ../../tools/combine_features.py --seqlens {input.seqlens} --bed utr {input.utrs} --bed intron {input.introns} --bed cds {input.cds} --bed phastcons {input.phastcons} | \
      gzip > {output}
    """

rule merged_utrs_cds_introns_unlabelled:
  input: utrs="hg38.ensembl_utrs.bed.gz", introns="hg38.ensembl_introns.bed.gz", cds="hg38.ensembl_cds.bed.gz", seqlens="hg38_seqlens.tsv"
  output: "merged_utrs_cds_introns_unlabelled.bed.gz"
  shell:
    """
    python ../../tools/combine_features.py --seqlens {input.seqlens} --bed utr {input.utrs} --bed intron {input.introns} --bed cds {input.cds} | \
      cut -f1-3 | \
      gzip > {output}
    """


rule merged_utrs_cds_introns_phastcons_unlabelled:
  input: utrs="hg38.ensembl_utrs.bed.gz", introns="hg38.ensembl_introns.bed.gz", cds="hg38.ensembl_cds.bed.gz", phastcons="conserved_phastcons_thresh0.bed.gz", seqlens="hg38_seqlens.tsv"
  output: "merged_utrs_cds_introns_phastcons_unlabelled.bed.gz"
  shell:
    """
    python ../../tools/combine_features.py --seqlens {input.seqlens} --bed utr {input.utrs} --bed intron {input.introns} --bed cds {input.cds} --bed phastcons {input.phastcons} | \
      cut -f1-3 | \
      gzip > {output}
    """


## === Simplified Annotation Tracks for Sims
# we want simple, example conservation tracks, but semi-realistic, e.g. CDS
sim_cons = ["conserved_phastcons_thresh0_chr10.bed",  # this is currently the primary conserved track for simulations
            "merged_utrs_cds_introns_phastcons_chr10.bed",
            ]
sim_neut = ["neutral_phastcons_thresh0_chr10.bed.gz"]

# sepearate out conserved tracks into separate chromosome files
rule split_conserved:
  input: "conserved_phastcons_thresh{thresh}.bed.gz"
  output: "conserved_phastcons_thresh{thresh}_chr10.bed"
  shell:
      """
      zgrep "chr10" {input} > {output}
      """

rule split_merged:
  input: "merged_utrs_cds_introns_phastcons.bed.gz"
  output: "merged_utrs_cds_introns_phastcons_chr10.bed"
  shell:
      """
      zgrep "chr10" {input} | cut -f1-3 > {output}
      """

rule sim_neutral:
   # for the only phastcons-only sims
   input: conserved="conserved_phastcons_thresh{thresh}_chr10.bed", seqlens="hg38_seqlens.tsv"
   output: "neutral_phastcons_thresh{thresh}_chr10.bed.gz"
   shell:
      """
       cut -f1-3 {input.conserved} | \
       sort --version-sort -k1,1 -k2,2n | \
       bedtools merge | \
       grep chr10 | \
       bedtools complement -L -i - -g {input.seqlens} | \
       gzip > {output}
      """

## === Calcbkgd files
calc_bkgd = ["hg38_chr10_seqlens.tsv", "conserved_phastcons_thresh0/chr10.bed"]
# only chr10
rule seqlens_chr10:
  input: "hg38_seqlens.tsv"
  output: "hg38_chr10_seqlens.tsv"
  shell:
    """
      grep "chr10" {input} > {output}
    """

rule chr10_conserved:
  input: "conserved_phastcons_thresh0_chr10.bed"
  output: "conserved_phastcons_thresh0/chr10.bed"
  shell:
    """
    mkdir -p conserved_phastcons_thresh0
    cp {input} conserved_phastcons_thresh0/chr10.bed
    """


## === Accessible Regions
accessible = ["no_centro.bed"]

rule no_centro:
  input: centro="hg38_centro_slop.bed", seqlens="hg38_seqlens.tsv"
  output: "no_centro.bed"
  shell:
    """
    bedtools complement -i {input.centro} -g {input.seqlens} > {output}
    """

## === Neutral Regions
neutral = ["neutral_phastcons_genes_chr10.bed", "neutral_phastcons_genes.bed.gz"]

# Putatively neutral regions -- here we merge all phastcons and genes.
# Note, the last step is to take the complement of the conserved regions = putatively netural regions
# Importantly, below, we add in introns since these are neutralish-ish and make up a huge part of the 
# human genome.
rule neutral:
   input: conserved="phastConsElements100way.bed.gz", genes="hg38.ensembl_genes.bed.gz", seqlens="hg38_seqlens.tsv"
   output: "neutral_phastcons_genes.bed.gz"
   shell:
      """
      zcat {input.conserved} {input.genes} | \
       cut -f1-3 | \
       sort --version-sort -k1,1 -k2,2n | \
       bedtools slop -g {input.seqlens} -b 1000  | \
       bedtools merge | \
       bedtools complement -i - -g {input.seqlens} | gzip > {output}
      """

rule neutral_introns:
   # we add back in the introns as these make up ~40% of the genome and are neutralish
   input: neut="neutral_phastcons_genes.bed.gz", intron="hg38.ensembl_introns_conservative.bed.gz"
   output: "neutral_phastcons_genes_with_introns.bed.gz"
   shell:
     """
     zcat {input.neut} {input.intron} | sort --version-sort -k1,1 -k2,2n | \
      gzip > {output}
     """

# putatively neutral regions for sims, only chr10, simply features -- not conserved tracks 
rule neutral_chr10:
   input: conserved="neutral_phastcons_genes.bed.gz", seqlens="hg38_seqlens.tsv"
   output: "neutral_phastcons_genes_chr10.bed"
   shell:
      """
      bedtools complement -i {input.conserved} -g {input.seqlens} | grep "^chr10" > {output}
      """

rec = decode + hapmap 

## === Main target
print(annot + rec + cons + sim_cons + neutral + accessible + genome)
rule all:
  input: annot + rec + cons + sim_cons + neutral + accessible + genome + sim_neut + calc_bkgd


